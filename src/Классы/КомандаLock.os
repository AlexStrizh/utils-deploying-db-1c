Перем Лог;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("b ibconnection", "", "строка подключения к информационной базе /S<адрес сервера>\<имя базы>")
					.ТСтрока()
					.ВОкружении("CU_LOCK_IB CU_LOCK_IBCONNECTION");

	Команда.Опция("u db-user", "", "пользователь (с правом администрирования кластера серверов)")
	 				.ТСтрока()
	 				.ВОкружении("CU_LOCK_CLUSTER_USER");

	Команда.Опция("p db-pwd", "", "пароль пользователя (с правом администрирования кластера серверов)")
	 				.ТСтрока()
					.ВОкружении("CU_LOCK_CLUSTER_PASSWORD CU_LOCK_CLUSTER_PWD");

	Команда.Опция("t try", "", "количество попыток блокировки пользователей")
					.ТЧисло()
					.ВОкружении("CU_LOCK_TRY")
				   	.ПоУмолчанию(3);
					 
	Команда.Опция("c uccode", , "ключ разрешения запуска")
					.ТСтрока()
				   	.ВОкружении("CU_LOCK_UCCODE")
				   	.Обязательный(Ложь);

	Команда.Опция("m lockmessage", , "cообщение блокировки")
				   	.ТСтрока()
				  	.ВОкружении("CU_LOCK_LOCKMESSAGE CU_LOCK_MESSAGE")
				  	.Обязательный(Ложь);
					  
	Команда.Опция("s lockstart", , "время старта блокировки пользователей, время указываем как '2040-12-31T23:59:59'")
					.ТСтрока()
					.ВОкружении("CU_LOCK_LOCKSTART")
					.Обязательный(Ложь);

	Команда.Опция("e lockend", , "время окончания блокировки пользователей, время указываем как '2040-12-31T23:59:59'")
					.ТСтрока()
					.ВОкружении("CU_LOCK_LOCKEND CU_LOCK_LOCKSTOP")
					.Обязательный(Ложь);

	Команда.Аргумент("CONFIG", , "путь к файлу настройки подключений")
					.ТСтрока()
					.ВОкружении("CU_LOCK_CONFIG")
					.Обязательный(Ложь)
					.ПоУмолчанию(ПараметрыПриложения.ПолучитьПолноеИмяКФайлуНастроекПодключения());

	Команда.Спек = "(CONFIG) | (-b [-u -p] [-t] [-c] [-m] [-s] [-e]) | (-b [-u -p] [-t] [-c] [-m] [-s] [-e] CONFIG)";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Лог.Информация("Начало выполнение команды <lock>");

	ПутьКФайлуНастроек = Команда.ЗначениеАргумента("CONFIG");
	
	Лог.Отладка(ПутьКФайлуНастроек);

	ОбщиеПараметры = ПараметрыПриложения.Параметры();

	СтрокаПодключенияКБазе				= Команда.ЗначениеОпции("ibconnection");
	АдминистраторКластера				= Команда.ЗначениеОпции("db-user");
	ПарольАдминистратораКластера		= Команда.ЗначениеОпции("db-pwd");
	КоличествоПопыток					= Команда.ЗначениеОпции("try");
	КодРазрешения						= Команда.ЗначениеОпции("uccode");
	СообщениеБлокировки					= Команда.ЗначениеОпции("lockmessage");
	ВремяСтартаБлокировки				= Команда.ЗначениеОпции("lockstart");
	ВремяОкончанияБлокировки			= Команда.ЗначениеОпции("lockend");

	Лог.Отладка("Установлена опция <%1> со значением <%2>", "ibconnection", СтрокаПодключенияКБазе);
	Лог.Отладка("Установлена опция <%1> со значением <%2>", "db-user", АдминистраторКластера);
	Лог.Отладка("Установлена опция <%1> со значением <%2>", "db-pwd", ПарольАдминистратораКластера);
	Лог.Отладка("Установлена опция <%1> со значением <%2>", "try", КоличествоПопыток);
	Лог.Отладка("Установлена опция <%1> со значением <%2>", "uccode", КодРазрешения);
	Лог.Отладка("Установлена опция <%1> со значением <%2>", "lockmessage", СообщениеБлокировки);
	Лог.Отладка("Установлена опция <%1> со значением <%2>", "lockstart", ВремяСтартаБлокировки);
	Лог.Отладка("Установлена опция <%1> со значением <%2>", "lockend", ВремяОкончанияБлокировки);

	МенеджерКоманд = Новый МенеджерКоманд();

	МенеджерКоманд.УровеньЛога(ПараметрыПриложения.УровеньЛога())
					.ВерсияПлатформы(ОбщиеПараметры.ВерсияПлатформы)
					.АвторизацияНаКластереСерверов(АдминистраторКластера, ПарольАдминистратораКластера)
					.СтрокаПодключения(СтрокаПодключенияКБазе)
					.КоличествоПопытокОтключения(КоличествоПопыток)
					.УстановитьКодРазрешенияЗапуска(КодРазрешения)
					.УстановитьСообщениеБлокировки(СообщениеБлокировки)
					.УстановитьВремяСтартаБлокировки(ВремяСтартаБлокировки)
					.УстановитьВремяОкончанияБлокировки(ВремяОкончанияБлокировки);
	
	ПереопределитьПустыеПараметрыЗначениямиИзФайлаНастроек(МенеджерКоманд, ПутьКФайлуНастроек);

	МенеджерКоманд.ВыполнитьБлокировкуПользователей();

	Лог.Информация("Завершено выполнение команды <lock>");
		
КонецПроцедуры

#Область Служебные_процедуры_и_функции

Процедура ПереопределитьПустыеПараметрыЗначениямиИзФайлаНастроек(Знач МенеджерКоманд, Знач ПутьКФайлуНастроек)
	Если НЕ ПустаяСтрока(ПутьКФайлуНастроек) Тогда

		ФайлНастроек = Новый Файл(ПутьКФайлуНастроек);

		Если ФайлНастроек.Существует() Тогда

			МенеджерКоманд.УстановитьНастройки(ПутьКФайлуНастроек);
			МенеджерКоманд.ПрочитатьФайлНастроек();

			Параметры = МенеджерКоманд.Параметры();

			Если НЕ ЗначениеЗаполнено(МенеджерКоманд.ПолучитьАдминистратораКластера()) Тогда
				Пользователь = Параметры.НастройкиПоУмолчанию.АдминистраторКластера;
				Пароль = Параметры.НастройкиПоУмолчанию.ПарольАдминистратораКластера;
				МенеджерКоманд.АвторизацияНаКластереСерверов(Пользователь, Пароль);
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(МенеджерКоманд.ПолучитьСтрокуПодключенияКБазе()) Тогда
				МенеджерКоманд.СтрокаПодключения(Параметры.НастройкиПоУмолчанию.СтрокаПодключенияКБазе);
			КонецЕсли;

			Лог.Отладка("Обработана настройка <%1>, установлен параметр <%2>", "АдминистраторКластера", МенеджерКоманд.ПолучитьАдминистратораКластера());
			Лог.Отладка("Обработана настройка <%1>, установлен параметр <%2>", "ПарольАдминистратораКластера", МенеджерКоманд.ПолучитьПарольАдминистратораКластера());
			Лог.Отладка("Обработана настройка <%1>, установлен параметр <%2>", "СтрокаПодключения", МенеджерКоманд.ПолучитьСтрокуПодключенияКБазе());

		Иначе
			ВызватьИсключение Новый ИнформацияОбОшибке(СтрШаблон("Файл настроек <%1> не найден", ПутьКФайлуНастроек), );
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Лог = ПараметрыПриложения.Лог();